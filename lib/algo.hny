from bag_net import *
construct(0,1,0)

const NIDS = 3   # number of identifiers
leader = 0
def processor(self, succ):
	send(self,succ,False)
	var working = True
	while working:
		atomically:
			var req = receive(self)
			if (len(req) > 0):
				var id, found = req[0].src,req[0].payload
				if id == self:
					assert self == leader
					send(id,succ, True)
				elif id > self:
					assert self != leader
					send(id,succ, found)
				if found:
					working = False

ids, nprocs, procs = { 1 .. NIDS }, choose({ 1 .. NIDS }), []
for i in { 0 .. nprocs - 1 }:
    let next = choose(ids):
        ids -= { next }
        procs += [ next, ]
        if next > leader:
            leader = next
for i in { 0 .. nprocs - 1 }:
    spawn processor(procs[i], procs[(i + 1) % nprocs])
